buildscript {
    repositories {
        mavenLocal()
        maven {
            url "${artifactory_contextUrl_pluginRel}"
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'idea'
    id 'checkstyle'
    id 'maven-publish'
    id "com.jfrog.artifactory" version "4.11.0"
    id 'net.researchgate.release' version '2.6.0'
}

group 'com.capgemini'
description = """Spring-Boot-Camel Metrics Publisher """

repositories {
    mavenLocal()
    maven {
        url "${artifactory_contextUrl_libReleaseLocal}"
        credentials {
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
    }
    maven {
        url "${artifactory_contextUrl_libRelease}"
        credentials {
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
    }
    maven {
        url "${artifactory_contextUrl_libSnapshot}"
        credentials {
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
    }
    maven {
        url "${artifactory_contextUrl_libSnapshotLocal}"
        credentials {
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
    }
}

dependencies {
    implementation     group: 'org.springframework.boot' , name: 'spring-boot',                        version: '2.2.0.RELEASE' // need to exclude things we DON'T NEED here
    implementation     group: 'org.apache.camel',          name: 'camel-core',                         version: '3.0.0'
    implementation     group: 'io.dropwizard.metrics',     name: 'metrics-jvm',                        version: '4.1.1'
    implementation     group: 'io.dropwizard.metrics',     name: 'metrics-jmx',                        version: '4.1.1'
    implementation     group: 'io.dropwizard.metrics',     name: 'metrics-jetty9',                     version: '4.1.1'
    implementation     group: 'io.dropwizard.metrics',     name: 'metrics-servlets',                   version: '4.1.1'
    implementation     group: 'com.netflix.hystrix',       name: 'hystrix-core',                       version: '1.5.18'
    implementation     group: 'com.netflix.hystrix',       name: 'hystrix-codahale-metrics-publisher', version: '1.5.18'
    implementation     group: 'com.netflix.hystrix',       name: 'hystrix-metrics-event-stream',       version: '1.5.18'
    implementation     (group: 'com.bealetech',             name: 'metrics-statsd',                    version: '3.0.2')
            {
                exclude group: 'com.codahale.metrics', module: 'metrics-core'
            }
    implementation     group: 'com.capgemini',             name: 'codahale-metrics-filters',           version: '0.13.0-SNAPSHOT'

    testImplementation group: 'org.junit.jupiter',         name: 'junit-jupiter-engine',               version: '5.5.2'
}

release {
    tagTemplate = '${name}-${version}'
    buildTasks = ['clean', 'build', 'artifactoryPublish']

    git {
        requireBranch = 'release'
    }
}

afterReleaseBuild.dependsOn artifactoryPublish

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            if (project.version.endsWith('-SNAPSHOT')) {
                repoKey = 'libs-snapshot-local'
            } else {
                repoKey = 'libs-release-local'
            }

            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
